#Область ОписаниеПеременных

&НаКлиенте
Перем РаботаСМетаданными;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Инициализировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьРеквизитыОбъекта();
	//@skip-check use-non-recommended-method
	РаботаСМетаданными = ПолучитьФорму(Объект.ПутьКФормам + "РаботаСМетаданными",,,
		Новый УникальныйИдентификатор());
	РаботаСМетаданными.ЗагрузитьПараметрыВыгрузки(ЭтотОбъект, ПолноеИмя, Раздел, ТекстНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьПараметрыВыгрузки(Команда)
	
	ЗакрытьФормуИПередатьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияФормы

#Область Общие

&НаКлиенте
Процедура ЗакрытьФормуИПередатьНастройки()
	
	ТекстНастроек = РаботаСМетаданными.СохранитьПараметрыВыгрузкиВСтроку(ЭтотОбъект);
	Закрыть(ТекстНастроек);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыОбъекта()
	
	ТекущиеДанные = ВладелецФормы.ОбъектыМетаданных.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ЭлементыОбъекта = РеквизитыОбъекта.ПолучитьЭлементы();
	
	РазделыТекущегоОбъекта = ТекущиеДанные.ПолучитьЭлементы();
	Для Каждого РазделОбъекта Из РазделыТекущегоОбъекта Цикл
		ЭлементыТекущегоОбъекта = РазделОбъекта.ПолучитьЭлементы();
		Для Каждого ЭлементОбъекта Из ЭлементыТекущегоОбъекта Цикл
			Если Не ЭлементОбъекта.ЭтоПоле
				И Не ЭлементОбъекта.ЭтоТаблица
				Или ЭлементОбъекта.СтандартныйРеквизит Тогда
				Продолжить;
			КонецЕсли;
			НовыйЭлемент = ЭлементыОбъекта.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементОбъекта);
			НовыйЭлемент.Выгружать = Истина;
			Если Не ЭлементОбъекта.ЭтоТаблица Тогда
				Продолжить;
			КонецЕсли;
			РеквизитыТаблицы = НовыйЭлемент.ПолучитьЭлементы();
			ЭлементыТаблицыОбъекта = ЭлементОбъекта.ПолучитьЭлементы();
			Для Каждого ЭлементТаблицы Из ЭлементыТаблицыОбъекта Цикл
				Если ЭлементТаблицы.СтандартныйРеквизит Тогда
					Продолжить;
				КонецЕсли;
				НовыйЭлементТаблицы = РеквизитыТаблицы.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйЭлементТаблицы, ЭлементТаблицы);
				НовыйЭлементТаблицы.Выгружать = Истина;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура Инициализировать()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Объект.ПутьКФормам = ОбработкаОбъект.Метаданные().ПолноеИмя() + ".Форма.";
	
	ЗаполнитьПараметры();
	
КонецПроцедуры

#КонецОбласти

#Область Частные

&НаСервере
Процедура ЗаполнитьПараметры()
	
	ПолноеИмя           = Параметры.ПолноеИмя;
	Раздел              = Параметры.Раздел;
	ТекстНастроек       = Параметры.ТекстНастроек;
	ИдентификаторСтроки = Параметры.ИдентификаторСтроки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти