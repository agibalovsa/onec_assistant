#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолноеИмяОбъекта = Параметры.ПолноеИмяОбъекта;
	Раздел = Параметры.Раздел;
	
	ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	Если ЧастиИмени.Количество() > 2
		 ИЛИ СтрНачинаетсяС(ЧастиИмени[0], "Регистр") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МенеджерОбъекта = ПолучитьМенеджерОбъекта(Раздел, ЧастиИмени[1]);
	
	ЛюбаяСсылка = МенеджерОбъекта.ПустаяСсылка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОпределитьСсылку(Команда)
	
	ЛюбаяСсылка = ОпределитьСсылкуНаСервере(УникальныйИдентификаторОбъекта, Раздел, ПолноеИмяОбъекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьУникальныйИдентификатор(Команда)
	
	УникальныйИдентификаторОбъекта = ОпределитьУникальныйИдентификаторНаСервере(ЛюбаяСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ОпределитьУникальныйИдентификаторНаСервере(Ссылка)
	
	Возврат Ссылка.УникальныйИдентификатор();
	
КонецФункции

&НаСервереБезКонтекста
Функция ОпределитьСсылкуНаСервере(УникальныйИдентификаторОбъекта, Раздел, ПолноеИмяОбъекта)
	
	ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	
	МенеджерОбъекта = ПолучитьМенеджерОбъекта(Раздел, ЧастиИмени[1]);
	
	Ссылка = МенеджерОбъекта.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторОбъекта));
	
	Возврат Ссылка;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьМенеджерОбъекта(Раздел, ИмяОбъекта)
	
	//@skip-check server-execution-safe-mode
	Менеджер = Вычислить(Раздел);
	Возврат Менеджер[ИмяОбъекта];
	
КонецФункции

#КонецОбласти